#include "imports/stdlib.fc";
#include "types.func";

{-
  Helpers below fill in default/overwritten values of message layout:
  Relevant part of TL-B schema:
  ... other:ExtraCurrencyCollection ihr_fee:Grams fwd_fee:Grams created_lt:uint64 created_at:uint32 = CommonMsgInfoRelaxed;
  bits      1                               4             4                64                32                            
  ... init:(Maybe (Either StateInit ^StateInit))  body:(Either X ^X) = Message X;
  bits      1      1(if prev is true)                   1

-}

builder store_msgbody_prefix_stateinit(builder b, cell state_init, cell ref) inline {
    return b.store_uint(4 + 2 + 1, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1).store_ref(state_init).store_ref(ref);
}

builder store_msgbody_prefix_stateinit_slice(builder b, cell state_init) inline {
    return b.store_uint(4 + 2 + 0, 1 + 4 + 4 + 64 + 32 + 1 + 1 + 1).store_ref(state_init);
}

builder store_msgbody_prefix_slice(builder b) inline {
    return b.store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1);
}
builder store_msgbody_prefix_ref(builder b, cell ref) inline {
    return b.store_uint(1, 1 + 4 + 4 + 64 + 32 + 1 + 1).store_ref(ref);
}

() send_msg_builder(slice to_address, int amount, builder payload, int flags, int send_mode) impure inline_ref {
    int payload_length = payload.null?() ? 0 : payload.builder_bits();

    builder msg = begin_cell()
        .store_msg_flags_and_address_none(flags)
        .store_slice(to_address)
        .store_coins(amount);

    if (payload_length + msg.builder_bits() > 1023 - (1 + 4 + 4 + 64 + 32 + 1 + 1)) {
        msg = msg.store_msgbody_prefix_ref(begin_cell().store_builder(payload).end_cell());
    } else {
        msg = msg.store_msgbody_prefix_slice()
                 .store_builder(payload);
    }

    send_raw_message(msg.end_cell(), send_mode);
}


() send_msg_builder_with_stateinit(slice to_address, int amount, cell state_init, builder payload, int flags, int send_mode) impure inline_ref {
    int payload_length = payload.null?() ? 0 : payload.builder_bits();

    builder msg = begin_cell()
    .store_msg_flags_and_address_none(flags)
    .store_slice(to_address)
    .store_coins(amount);

    if (payload_length + msg.builder_bits() > 1023 - (1 + 4 + 4 + 64 + 32 + 1 + 1)) {
        msg = msg.store_msgbody_prefix_stateinit(state_init, begin_cell().store_builder(payload).end_cell());
    } else {
        msg = msg.store_msgbody_prefix_stateinit_slice(state_init)
        .store_builder(payload);
    }

    send_raw_message(msg.end_cell(), send_mode);
}
